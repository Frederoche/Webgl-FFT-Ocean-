<!DOCTYPE html>
<head>
<link href="css/styles.css" rel="stylesheet"/>
    <link rel="stylesheet" href="https://maxcdn.bootstrapcdn.com/bootstrap/4.0.0-alpha.6/css/bootstrap.min.css" integrity="sha384-rwoIResjU2yc3z8GV/NPeZWAv56rSmLldC3R/AZzGRnGxQQKnKkoFVhFQhNUwEyJ" crossorigin="anonymous">
    <script src="https://npmcdn.com/tether@1.2.4/dist/js/tether.min.js"></script>
    <script
    src="https://code.jquery.com/jquery-3.2.1.min.js"
    integrity="sha256-hwg4gsxgFZhOsEEamdOYGBf13FyQuiTwlAQgxVSNgt4="
    crossorigin="anonymous"></script>
    <script src="https://maxcdn.bootstrapcdn.com/bootstrap/4.0.0-alpha.6/js/bootstrap.min.js" integrity="sha384-vBWWzlZJ8ea9aCX4pEW3rVHjgjt7zpkNpZk+02D9phzyeVkE+jo0ieGizqPLForn" crossorigin="anonymous"></script>
    <script src="Javascript/Ocean.js"></script>
    
    <script src="gl-matrix.js"></script>
	<meta name="viewport" content="width=device-width, initial-scale=1">

<script id="skyBoxVertexShader" type="x-shader/x-vertex">
        precision mediump float; 
        uniform mat4 projMatrix; 
        uniform mat4 viewMatrix; 

        attribute vec2 texCoord; 
        attribute vec3 position; 

        varying vec2 texcoord; 
        varying vec3 wPos; 
        varying vec4 scaledPos;

        varying float size;
        
        void main(void) 
        { 
            texcoord = texCoord;    
            wPos = position; 
			size = 2000.0;

            scaledPos =   vec4(position.x * size, position.y * size  ,position.z * size , 1.0) ;
            
            gl_Position = projMatrix * viewMatrix * vec4(position.x* size, position.y * size ,position.z * size, 1.0);
            
        }

    </script>
<script id="skyBoxfragmentShader" type="x-shader/x-fragment">
        precision highp float;

        varying vec2 texcoord; 
        varying vec3 wPos; 
        varying vec4 scaledPos;
		varying float size;

        uniform sampler2D texture; 
        uniform vec3 cameraPosition;
        
        uniform vec4 clipplane;
        uniform float isclipped;

		const float Pi = 3.1415;
	

        vec3 grad(float dist, vec3 lightColor){ 
            return max(exp(-pow(dist, 0.85)) * lightColor - 0.1, 0.0); 
        } 
        
        
        void main(void) 
        { 
			vec4 LIGHT_POSITION = vec4(1000.0,30.0,-1900.0, 1.0);
	  
			float lightDistance = length(LIGHT_POSITION.xyz);
	
			float clampedDistance= clamp(lightDistance, 0.0, 1.0);
			float sun = clamp(1.0 - smoothstep(0.1, 0.1, clampedDistance), 0.0, 1.0);
	
			float h = wPos .y + 0.01;
	        h = (0.25 * mix(clampedDistance, 1.0, h)) / h;
    
            vec3 gradient= grad(lightDistance, vec3(0.5));
		
		    vec3 color = h * vec3(0.32, 0.6, 1.0);
    
    	    color = max(color, 0.0);
    
		    color = max(mix(pow(color, 1.0 - color), color / (2.0 * color + 0.5 - color), clamp(LIGHT_POSITION.y* 2.0, 0.0, 1.0)),0.0) + sun + gradient;

            if(isclipped == 1.0 && dot(scaledPos, clipplane) >= 0.0)
            { 
                discard; 
            } 
	
			if(wPos.y < -0.01)
				gl_FragColor = vec4(0.05,0.2,0.25, 1.0);      
			else
				gl_FragColor = vec4(color, 1.0); //SKY COLOR
            
			//SUN
			for(float i=0.0; i < Pi*2.0;i+=0.1)
			{
				float x = cos(i)*0.025;
				float y = sin(i)*0.025;
	
				if(wPos.y -0.2 < y && wPos.x - 0.62 < x && wPos.x -0.62 > -x && wPos.y -0.2 > -y)
					gl_FragColor = vec4(1.0, 1.0, 0.5, 1.0)*2.0;
			}

	}
    </script>
<script id="vertexShader" type="x-shader/x-vertex">
        precision mediump float; 
        precision highp sampler2D;

        uniform mat4 projMatrix; 
        uniform mat4 viewMatrix;
        uniform mat4 birdviewMatrix;

        uniform vec3 cameraPosition; 

        uniform mat4 invProjMatrix;
        uniform mat4 invViewMatrix;
          
        uniform sampler2D displacement; 

        attribute vec3 position; 
        attribute vec2 texCoord; 
        
        varying vec2 texcoord; 
        varying vec4 clipSpace;
        
        varying vec4 wPosition; 
        varying vec4 pos; 
        varying vec3 camPos;
		
		
        void main(void)
        { 
            
			vec3 cameraSpace = normalize((invProjMatrix * vec4(position.x, position.z ,0.0, 1.0)).xyz);
            vec3 worldSpace  = (invViewMatrix * vec4(cameraSpace, 0.0)).xyz;

            float t = (-cameraPosition.y)/worldSpace.y ;

            vec3 u =  (cameraPosition + t * worldSpace);

            texcoord = (u.xz + 1.0) * 0.5;

			vec4 d  = texture2D(displacement, texcoord);
	    
            camPos = cameraPosition;
              
            //ATTENUATION HORIZONT
            float att = 2.0/pow(length(camPos - u), 1.0/2.0);
            
            u.y = (d.y) * att;
            u.x = (u.x + d.x);
            u.z = (u.z + d.z);
    		
            wPosition = vec4(u.x, u.y, u.z, 1.0);
                    
            clipSpace  = projMatrix * viewMatrix * vec4(u , 1.0); 
            
            pos = (viewMatrix * vec4(worldSpace , 1.0)); 
            
            if (t > 0.0) 
            {
                gl_Position = projMatrix * viewMatrix * vec4(u , 1.0);
            }
            else
            {
                gl_Position = vec4(0.0,0.0,0.0,0.0);
            }
         }
    </script>
<script id="fragmentShader" type="x-shader/x-fragment">
        precision highp float;
        precision highp sampler2D;

        varying vec2 texcoord; 

        uniform sampler2D displacement; 
        uniform sampler2D reflectionSampler;
        uniform sampler2D refractionSampler; 

        varying vec3 camPos;
        varying vec4 wPosition;
        varying vec4 normalWorld; 
        varying vec4 pos; 
        
        varying vec4 eyespacePos;
        varying vec4 clipSpace;


        vec3 calcNormals(sampler2D text, vec2 texcoords) { 

            float texel = 1.0 / 128.0;
            float texelSize =   2.0/ 128.0; 

            vec3 center = texture2D(text, texcoords).xyz; 

            vec3 left   = vec3(-texelSize, 0.0,0.0)  + texture2D(text, texcoords + vec2(-texel, 0.0)).xyz - center; 
            vec3 right  = vec3(texelSize, 0.0, 0.0)  + texture2D(text, texcoords + vec2(texel, 0.0)).xyz  - center; 
            vec3 bottom = vec3(0.0, 0.0, texelSize)  + texture2D(text, texcoords + vec2(0.0, texel)).xyz  - center;
            vec3 top    = vec3(0.0, 0.0, -texelSize) + texture2D(text, texcoords + vec2(0.0, -texel)).xyz - center; 

            vec3 topRight    = cross(right, top); 
            vec3 topLeft     = cross(top, left); 
            vec3 bottomLeft  = cross(left, bottom); 
            vec3 bottomRight = cross(bottom, right); 
    
            return normalize(bottomRight+bottomLeft+topLeft+topRight); 
        }
		
        void main(void) { 
            vec3 LIGHTCOLOR         = vec3(0.5, 0.5, 0.25); 
            vec3 SEA_WATER_COLOR    = vec3(0.05,0.2,0.25); 
            vec4 LIGHT_POSITION     = vec4(1000.0, 30.0,-1900.0, 1.0); 
			
			float PI = 3.1415;

            vec3 lightDir = normalize(LIGHT_POSITION.xyz); 
            vec3 view     = normalize(camPos - wPosition.xyz); 

            vec3 H = normalize(lightDir + view);
			vec3 normal  = calcNormals(displacement, texcoord);
			
			if (dot(view, normal) < 0.0) {
                normal = reflect(normal, -view);
            }
			
			//REFLECTION
            vec2 reflectionCoord;
            vec2 refractionCoord;
             
            reflectionCoord.x = 0.5*(clipSpace.w + clipSpace.x)/clipSpace.w;
            reflectionCoord.y = 0.5*(clipSpace.w + clipSpace.y)/clipSpace.w;	

			//DISTORTION
            vec4 reflectionTex = texture2D(reflectionSampler, reflectionCoord + normal.xz*0.5);

            vec3 refraction = SEA_WATER_COLOR; 
            vec3 reflection = reflectionTex.xyz;
			
			//COOK TORRANCE 
			float NdotL = max(dot(normal, lightDir), 0.0);
			
			float specularBRDF  = 0.0;
			float refractedBRDF = 0.0;
			float k = 0.1;
			
			//SPECULAR
			
			float NdotH = max(dot(normal, H), 0.0);
			float NdotV = max(dot(normal, view), 0.0);
			float VdotH = max(dot(view, H), 0.0);
			
			float NH2 = 2.0 * NdotH;
			float gb = (NH2 * NdotV) / VdotH;
			float gc = (NH2 * NdotL) / VdotH;
			float geometricalAttenuation = min(1.0, min(gb, gc));
			
			//DISTRIBUTION
			float constant = 1.0 / ( PI * 0.05 * pow(NdotH, 4.0));
			float exponant = (NdotH * NdotH - 1.0) / (0.05 * NdotH * NdotH);
			float roughness = constant * exp(exponant);
			
			float fresnelMat = pow(1.0 - VdotH, 5.0)*0.02 + 0.98;
			
			if(NdotL>0.0)
			{
				specularBRDF = (fresnelMat * geometricalAttenuation * roughness) / (NdotV * NdotL * 4.0);
			}
			
			vec3 cook =  LIGHTCOLOR * NdotL * (specularBRDF * (1.0 - k));
			
			float fresnelWater = 0.02 + 0.98 * pow(1.0 - dot(normal, view), 5.0);
			
            vec3 color = cook + mix(refraction/PI, reflection, fresnelWater) + vec3(0.1,0.1,0.1);
            
			//HDR
			color = vec3(1.0) - exp(-1.2*color);
			
            gl_FragColor = vec4(color, 1.0);

			
    }
     </script>
</head>

<body>
<div id="panel" class="container">
    <div class="form-group">
        <img id="spectrum" class="img-control"/>
    </div>
    <div class="form-group">
        <img id="hu" class="img-control"/>
    </div>
    <div class="form-group">
        <img id="spacial" class="img-control"/>
    </div>
    <div class="form-group">
        <label id="cheight-lbl">Camera height</label>
        <input type="text" id="camera-height" class="form-control" disabled/>
    </div>
    <div class="form-group">
        <label id="choppiness-lbl" for="choppiness">Choppiness</label>
        <input id="choppiness" type="range" min="0" max="10" value="9" class="form-control"/>
    </div>
    <div class="form-group">
        <label class="custom-control custom-checkbox" for="wireframe" id="wireframe-lbl">
            <input type="checkbox" class="custom-control-input" type="checkbox" id="wireframe" class="form-control">
            <span class="custom-control-indicator"></span>
            <span class="custom-control-description">Wireframe toggle</span>
          </label>
    </div>
    </div>
</div>
<canvas id="canvas"></canvas>
</body>
</html>